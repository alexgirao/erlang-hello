<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
   "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<!-- This document was generated using DocBuilder-0.9.8.4 -->
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
  <title>Robustness</title>
  <meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1"/>
  <script type="text/javascript" src="../../doc/erlresolvelinks.js"></script>
  <style type="text/css">
<!--
    body          { font-family: Verdana, Arial, Helvetica, sans-serif }
    span.bold_code        { font-family: courier;font-weight: bold}
    span.code        { font-family: courier;font-weight: normal}

.note, .warning {
  border: solid black 1px;
  margin: 1em 3em;
}

.note .label {
  background: #30d42a;
  color: white;
  font-weight: bold;
  padding: 5px 10px;
}
.note .content {
  background: #eafeea;
  color: black;
  line-height: 120%;
  font-size: 90%;
  padding: 5px 10px;
}
.warning .label {
  background: #C00;
  color: white;
  font-weight: bold;
  padding: 5px 10px;
}
.warning .content {
  background: #FFF0F0;
  color: black;
  line-height: 120%;
  font-size: 90%;
  padding: 5px 10px;
}

    .example     { background-color:#eeeeff } 
    pre          { font-family: courier; font-weight: normal }
    .REFBODY     { margin-left: 13mm }
    .REFTYPES    { margin-left: 8mm }
-->
  </style>
</head>
<body bgcolor="#FFFFFF" text="#000000" link="#0000FF" vlink="#FF00FF" alink="#FF0000">
<center>
<a href="http://www.ericsson.com/technology/opensource/erlang"><img border="0" alt="[Ericsson AB]" src="min_head.gif"/></a>
</center><a name="4"><!-- Empty --></a>
<h2>4 Robustness</h2>

<p>There are several things which are wrong with
the <a href="conc_prog.html#ex">messenger example</a> from
the previous chapter. For example if a node where a user is logged
on goes down without doing a log off, the user will remain in
the server's <span class="code">User_List</span> but the client will disappear thus
making it impossible for the user to log on again as the server
thinks the user already logged on.
</p>
<p>Or what happens if the server goes down in the middle of sending a
message leaving the sending client hanging for ever in
the <span class="code">await_result</span> function?
</p><a name="4.1"><!-- Empty --></a>
<h3>4.1 Timeouts</h3>

<p>Before improving the messenger program we will look into some
general principles, using the ping pong program as an example.
Recall that when &#34;ping&#34; finishes, it tells &#34;pong&#34; that it has
done so by sending the atom <span class="code">finished</span> as a message to &#34;pong&#34;
so that &#34;pong&#34; could also finish. Another way to let &#34;pong&#34;
finish, is to make &#34;pong&#34; exit if it does not receive a message
from ping within a certain time, this can be done by adding a
<strong>timeout</strong> to <span class="code">pong</span> as shown in the following example:
</p>
<div class="example"><pre>
-module(tut19).

-export([start_ping/1, start_pong/0,  ping/2, pong/0]).

ping(0, Pong_Node) -&#62;
    io:format(&#34;ping finished~n&#34;, []);

ping(N, Pong_Node) -&#62;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&#62;
            io:format(&#34;Ping received pong~n&#34;, [])
    end,
    ping(N - 1, Pong_Node).

pong() -&#62;
    receive
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong()
    after 5000 -&#62;
            io:format(&#34;Pong timed out~n&#34;, [])
    end.

start_pong() -&#62;
    register(pong, spawn(tut19, pong, [])).

start_ping(Pong_Node) -&#62;
    spawn(tut19, ping, [3, Pong_Node]).
</pre></div>

<p>After we have compiled this and copied the <span class="code">tut19.beam</span>
file to the necessary directories:
</p>
<p>On (pong@kosken):
</p>
<div class="example"><pre>
(pong@kosken)1&#62; <strong>tut19:start_pong().</strong>
true
Pong received ping
Pong received ping
Pong received ping
Pong timed out
</pre></div>

<p>On (ping@gollum):
</p>
<div class="example"><pre>
(ping@gollum)1&#62; <strong>tut19:start_ping(pong@kosken).</strong>
&#60;0.36.0&#62;
Ping received pong
Ping received pong
Ping received pong
ping finished   
</pre></div>

<p>(The timeout is set in:
</p>
<div class="example"><pre>
pong() -&#62;
    receive
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong()
    after 5000 -&#62;
            io:format(&#34;Pong timed out~n&#34;, [])
    end.
</pre></div>

<p>We start the timeout (<span class="code">after 5000</span>) when we enter
<span class="code">receive</span>. The timeout is canceled if <span class="code">{ping,Ping_PID}</span>
is received. If <span class="code">{ping,Ping_PID}</span> is not received,
the actions following the timeout will be done after 5000
milliseconds. <span class="code">after</span> must be last in the <span class="code">receive</span>,
i.e. preceded by all other message reception specifications in
the <span class="code">receive</span>. Of course we could also call a function which
returned an integer for the timeout:
</p>
<div class="example"><pre>
after pong_timeout() -&#62;
</pre></div>

<p>In general, there are better ways than using timeouts to
supervise parts of a distributed Erlang system. Timeouts are
usually appropriate to supervise external events, for example if
you have expected a message from some external system within a
specified time. For example, we could use a timeout to log a user
out of the messenger system if they have not accessed it, for
example, in ten minutes.
</p><a name="4.2"><!-- Empty --></a>
<h3>4.2 Error Handling</h3>

<p>Before we go into details of the supervision and error handling
in an Erlang system, we need see how Erlang processes terminate,
or in Erlang terminology, <strong>exit</strong>.
</p>
<p>A process which executes <span class="code">exit(normal)</span> or simply runs out
of things to do has a <strong>normal</strong> exit.
</p>
<p>A process which encounters a runtime error (e.g. divide by zero,
bad match, trying to call a function which doesn't exist etc)
exits with an error, i.e. has an <strong>abnormal</strong> exit. A
process which executes
<a href="javascript:erlhref('../../', 'kernel', 'erlang.html#exit-1');">exit(Reason)</a>
where <span class="code">Reason</span> is any Erlang term except the atom
<span class="code">normal</span>, also has an abnormal exit.
</p>
<p>An Erlang process can set up links to other Erlang processes. If
a process calls
<a href="javascript:erlhref('../../', 'kernel', 'erlang.html#link-1');">link(Other_Pid)</a>
it sets up a bidirectional link between itself and the process
called <span class="code">Other_Pid</span>. When a process terminates its sends
something called a <strong>signal</strong> to all the processes it has
links to.
</p>
<p>The signal carries information about the pid it was sent from and
the exit reason.
</p>
<p>The default behaviour of a process which receives a normal exit
is to ignore the signal.
</p>
<p>The default behaviour in the two other cases (i.e. abnormal exit)
above is to bypass all messages to the receiving process and to
kill it and to propagate the same error signal to the killed
process' links. In this way you can connect all processes in a
transaction together using links and if one of the processes
exits abnormally, all the processes in the transaction will be
killed. As we often want to create a process and link to it at
the same time, there is a special BIF,
<a href="javascript:erlhref('../../', 'kernel', 'erlang.html#spawn_link-1');">spawn_link</a>
which does the same as <span class="code">spawn</span>, but also creates a link to
the spawned process.
</p>
<p>Now an example of the ping pong example using links to terminate
&#34;pong&#34;:
</p>
<div class="example"><pre>
-module(tut20).

-export([start/1,  ping/2, pong/0]).

ping(N, Pong_Pid) -&#62;
    link(Pong_Pid),
    ping1(N, Pong_Pid).

ping1(0, _) -&#62;
    exit(ping);

ping1(N, Pong_Pid) -&#62;
    Pong_Pid ! {ping, self()},
    receive
        pong -&#62;
            io:format(&#34;Ping received pong~n&#34;, [])
    end,
    ping1(N - 1, Pong_Pid).

pong() -&#62;
    receive
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong()
    end.

start(Ping_Node) -&#62;
    PongPID = spawn(tut20, pong, []),
    spawn(Ping_Node, tut20, ping, [3, PongPID]).
</pre></div>

<div class="example"><pre>
(s1@bill)3&#62; <strong>tut20:start(s2@kosken).</strong>
Pong received ping
&#60;3820.41.0&#62;
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
</pre></div>

<p>This is a slight modification of the ping pong program where both
processes are spawned from the same <span class="code">start/1</span> function,
where the &#34;ping&#34; process can be spawned on a separate node. Note
the use of the <span class="code">link</span> BIF. &#34;Ping&#34; calls
<span class="code">exit(ping)</span> when it finishes and this will cause an exit
signal to be sent to &#34;pong&#34; which will also terminate.
</p>
<p>It is possible to modify the default behaviour of a process so
that it does not get killed when it receives abnormal exit
signals, but all signals will be turned into normal messages on
the format <span class="code">{'EXIT',FromPID,Reason}</span> and added to the end of
the receiving processes message queue. This behaviour is set by:
</p>
<div class="example"><pre>
process_flag(trap_exit, true)
</pre></div>

<p>There are several other process flags, see
<a href="javascript:erlhref('../../', 'kernel', 'erlang.html#process_flag-2');">erlang(3)</a>.
Changing the default behaviour of a process in this way is
usually not done in standard user programs, but is left to
the supervisory programs in OTP (but that's another tutorial).
However we will modify the ping pong program to illustrate exit
trapping.
</p>
<div class="example"><pre>
-module(tut21).

-export([start/1,  ping/2, pong/0]).

ping(N, Pong_Pid) -&#62;
    link(Pong_Pid), 
    ping1(N, Pong_Pid).

ping1(0, _) -&#62;
    exit(ping);

ping1(N, Pong_Pid) -&#62;
    Pong_Pid ! {ping, self()},
    receive
        pong -&#62;
            io:format(&#34;Ping received pong~n&#34;, [])
    end,
    ping1(N - 1, Pong_Pid).

pong() -&#62;
    process_flag(trap_exit, true), 
    pong1().

pong1() -&#62;
    receive
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong1();
        {'EXIT', From, Reason} -&#62;
            io:format(&#34;pong exiting, got ~p~n&#34;, [{'EXIT', From, Reason}])
    end.

start(Ping_Node) -&#62;
    PongPID = spawn(tut21, pong, []),
    spawn(Ping_Node, tut21, ping, [3, PongPID]).
</pre></div>

<div class="example"><pre>
(s1@bill)1&#62; <strong>tut21:start(s2@gollum).</strong>
&#60;3820.39.0&#62;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
pong exiting, got {'EXIT',&#60;3820.39.0&#62;,ping}
</pre></div>
<a name="4.3"><!-- Empty --></a>
<h3>4.3 The Larger Example with Robustness Added</h3>

<p>Now we return to the messenger program and add changes which
make it more robust:
</p>
<div class="example"><pre>
%%% Message passing utility.  
%%% User interface:
%%% login(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.
%%% logoff()
%%%     Logs off anybody at at node
%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.
%%%
%%% One node in the network of Erlang nodes runs a server which maintains
%%% data about the logged on users. The server is registered as &#34;messenger&#34;
%%% Each node where there is a user logged on runs a client process registered
%%% as &#34;mess_client&#34; 
%%%
%%% Protocol between the client processes and the server
%%% ----------------------------------------------------
%%% 
%%% To server: {ClientPid, logon, UserName}
%%% Reply {messenger, stop, user_exists_at_other_node} stops the client
%%% Reply {messenger, logged_on} logon was successful
%%%
%%% When the client terminates for some reason
%%% To server: {'EXIT', ClientPid, Reason}
%%%
%%% To server: {ClientPid, message_to, ToName, Message} send a message
%%% Reply: {messenger, stop, you_are_not_logged_on} stops the client
%%% Reply: {messenger, receiver_not_found} no user with this name logged on
%%% Reply: {messenger, sent} Message has been sent (but no guarantee)
%%%
%%% To client: {message_from, Name, Message},
%%%
%%% Protocol between the &#34;commands&#34; and the client
%%% ---------------------------------------------- 
%%%
%%% Started: messenger:client(Server_Node, Name)
%%% To client: logoff
%%% To client: {message_to, ToName, Message}
%%%
%%% Configuration: change the server_node() function to return the
%%% name of the node where the messenger server runs

-module(messenger).
-export([start_server/0, server/0, 
         logon/1, logoff/0, message/2, client/2]).

%%% Change the function below to return the name of the node where the
%%% messenger server runs
server_node() -&#62;
    messenger@super.

%%% This is the server process for the &#34;messenger&#34;
%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server() -&#62;
    process_flag(trap_exit, true),
    server([]).

server(User_List) -&#62;
    receive
        {From, logon, Name} -&#62;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {'EXIT', From, _} -&#62;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        {From, message_to, To, Message} -&#62;
            server_transfer(From, To, Message, User_List),
            io:format(&#34;list is now: ~p~n&#34;, [User_List]),
            server(User_List)
    end.

%%% Start the server
start_server() -&#62;
    register(messenger, spawn(messenger, server, [])).

%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&#62;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&#62;
            From ! {messenger, stop, user_exists_at_other_node},  %reject logon
            User_List;
        false -&#62;
            From ! {messenger, logged_on},
            link(From),
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&#62;
    lists:keydelete(From, 1, User_List).


%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&#62;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&#62;
            From ! {messenger, stop, you_are_not_logged_on};
        {value, {_, Name}} -&#62;
            server_transfer(From, Name, To, Message, User_List)
    end.

%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&#62;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&#62;
            From ! {messenger, receiver_not_found};
        {value, {ToPid, To}} -&#62;
            ToPid ! {message_from, Name, Message}, 
            From ! {messenger, sent} 
    end.

%%% User Commands
logon(Name) -&#62;
    case whereis(mess_client) of 
        undefined -&#62;
            register(mess_client, 
                     spawn(messenger, client, [server_node(), Name]));
        _ -&#62; already_logged_on
    end.

logoff() -&#62;
    mess_client ! logoff.

message(ToName, Message) -&#62;
    case whereis(mess_client) of % Test if the client is running
        undefined -&#62;
            not_logged_on;
        _ -&#62; mess_client ! {message_to, ToName, Message},
             ok
end.

%%% The client process which runs on each user node
client(Server_Node, Name) -&#62;
    {messenger, Server_Node} ! {self(), logon, Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&#62;
    receive
        logoff -&#62;
            exit(normal);
        {message_to, ToName, Message} -&#62;
            {messenger, Server_Node} ! {self(), message_to, ToName, Message},
            await_result();
        {message_from, FromName, Message} -&#62;
            io:format(&#34;Message from ~p: ~p~n&#34;, [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&#62;
    receive
        {messenger, stop, Why} -&#62; % Stop the client 
            io:format(&#34;~p~n&#34;, [Why]),
            exit(normal);
        {messenger, What} -&#62;  % Normal response
            io:format(&#34;~p~n&#34;, [What])
    after 5000 -&#62;
            io:format(&#34;No response from server~n&#34;, []),
            exit(timeout)
    end.
</pre></div>

<p>We have added the following changes:
</p>
<p>The messenger server traps exits. If it receives an exit signal,
<span class="code">{'EXIT',From,Reason}</span> this means that a client process has
terminated or is unreachable because:
</p>
<ul>

<li>
the user has logged off (we have removed the &#34;logoff&#34;
message),
</li>


<li>
the network connection to the client is broken,
</li>


<li>
the node on which the client process resides has gone down,
or
</li>


<li>
the client processes has done some illegal operation.
</li>


</ul>

<p>If we receive an exit signal as above, we delete the tuple,
<span class="code">{From,Name}</span> from the servers <span class="code">User_List</span> using
the <span class="code">server_logoff</span> function. If the node on which the server
runs goes down, an exit signal (automatically generated by
the system), will be sent to all of the client processes:
<span class="code">{'EXIT',MessengerPID,noconnection}</span> causing all the client
processes to terminate.
</p>
<p>We have also introduced a timeout of five seconds in
the <span class="code">await_result</span> function. I.e. if the server does not
reply within five seconds (5000 ms), the client terminates. This
is really only needed in the logon sequence before the client and
server are linked.
</p>
<p>An interesting case is if the client was to terminate before
the server links to it. This is taken care of because linking to a
non-existent process causes an exit signal,
<span class="code">{'EXIT',From,noproc}</span>, to be automatically generated as if
the process terminated immediately after the link operation.
</p><center>
<hr/>
<small>
  Copyright &copy; 1991-2008
  <a href="http://www.ericsson.com/technology/opensource/erlang">Ericsson AB</a><br/ >
</small>
</center></body>
</html>
