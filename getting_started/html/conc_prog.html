<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
   "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<!-- This document was generated using DocBuilder-0.9.8.4 -->
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
  <title>Concurrent Programming</title>
  <meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1"/>
  <script type="text/javascript" src="../../doc/erlresolvelinks.js"></script>
  <style type="text/css">
<!--
    body          { font-family: Verdana, Arial, Helvetica, sans-serif }
    span.bold_code        { font-family: courier;font-weight: bold}
    span.code        { font-family: courier;font-weight: normal}

.note, .warning {
  border: solid black 1px;
  margin: 1em 3em;
}

.note .label {
  background: #30d42a;
  color: white;
  font-weight: bold;
  padding: 5px 10px;
}
.note .content {
  background: #eafeea;
  color: black;
  line-height: 120%;
  font-size: 90%;
  padding: 5px 10px;
}
.warning .label {
  background: #C00;
  color: white;
  font-weight: bold;
  padding: 5px 10px;
}
.warning .content {
  background: #FFF0F0;
  color: black;
  line-height: 120%;
  font-size: 90%;
  padding: 5px 10px;
}

    .example     { background-color:#eeeeff } 
    pre          { font-family: courier; font-weight: normal }
    .REFBODY     { margin-left: 13mm }
    .REFTYPES    { margin-left: 8mm }
-->
  </style>
</head>
<body bgcolor="#FFFFFF" text="#000000" link="#0000FF" vlink="#FF00FF" alink="#FF0000">
<center>
<a href="http://www.ericsson.com/technology/opensource/erlang"><img border="0" alt="[Ericsson AB]" src="min_head.gif"/></a>
</center><a name="3"><!-- Empty --></a>
<h2>3 Concurrent Programming</h2>
<a name="3.1"><!-- Empty --></a>
<h3>3.1 Processes</h3>

<p>One of the main reasons for using Erlang instead of other
functional languages is Erlang's ability to handle concurrency
and distributed programming. By concurrency we mean programs
which can handle several threads of execution at the same time.
For example, modern operating systems would allow you to use a
word processor, a spreadsheet, a mail client and a print job all
running at the same time. Of course each processor (CPU) in
the system is probably only handling one thread (or job) at a
time, but it swaps between the jobs a such a rate that it gives
the illusion of running them all at the same time. It is easy to
create parallel threads of execution in an Erlang program and it
is easy to allow these threads to communicate with each other. In
Erlang we call each thread of execution a <strong>process</strong>.
</p>
<p>(Aside: the term &#34;process&#34; is usually used when the threads of
execution share no data with each other and the term &#34;thread&#34;
when they share data in some way. Threads of execution in Erlang
share no data, that's why we call them processes).
</p>
<p>The Erlang BIF <span class="code">spawn</span> is used to create a new process:
<span class="code">spawn(Module, Exported_Function, List of Arguments)</span>.
Consider the following module:
</p>
<div class="example"><pre>
-module(tut14).

-export([start/0, say_something/2]).

say_something(What, 0) -&#62;
    done;
say_something(What, Times) -&#62;
    io:format(&#34;~p~n&#34;, [What]),
    say_something(What, Times - 1).

start() -&#62;
    spawn(tut14, say_something, [hello, 3]),
    spawn(tut14, say_something, [goodbye, 3]).
</pre></div>

<div class="example"><pre>
5&#62; <strong>c(tut14).</strong>
{ok,tut14}
6&#62; <strong>tut14:say_something(hello, 3).</strong>
hello
hello
hello
done
</pre></div>

<p>We can see that function <span class="code">say_something</span> writes its first
argument the number of times specified by second argument. Now
look at the function <span class="code">start</span>. It starts two Erlang processes,
one which writes &#34;hello&#34; three times and one which writes
&#34;goodbye&#34; three times. Both of these processes use the function
<span class="code">say_something</span>. Note that a function used in this way by
<span class="code">spawn</span> to start a process must be exported from the module
(i.e. in the <span class="code">-export</span> at the start of the module).
</p>
<div class="example"><pre>
9&#62; <strong>tut14:start().</strong>
hello
goodbye
&#60;0.63.0&#62;
hello
goodbye
hello
goodbye
</pre></div>

<p>Notice that it didn't write &#34;hello&#34; three times and then
&#34;goodbye&#34; three times, but the first process wrote a &#34;hello&#34;,
the second a &#34;goodbye&#34;, the first another &#34;hello&#34; and so forth.
But where did the &#60;0.63.0&#62; come from? The return value of a
function is of course the return value of the last &#34;thing&#34; in
the function. The last thing in the function <span class="code">start</span> is
</p>
<div class="example"><pre>
spawn(tut14, say_something, [goodbye, 3]).
</pre></div>

<p><span class="code">spawn</span> returns a <strong>process identifier</strong>, or
<strong>pid</strong>, which uniquely identifies the process. So &#60;0.63.0&#62;
is the pid of the <span class="code">spawn</span> function call above. We will see
how to use pids in the next example.
</p>
<p>Note as well that we have used ~p instead of ~w in
<span class="code">io:format</span>. To quote the manual: &#34;~p Writes the data with
standard syntax in the same way as ~w, but breaks terms whose
printed representation is longer than one line into many lines
and indents each line sensibly. It also tries to detect lists of
printable characters and to output these as strings&#34;.
</p><a name="3.2"><!-- Empty --></a>
<h3>3.2 Message Passing</h3>

<p>In the following example we create two processes which send
messages to each other a number of times.
</p>
<div class="example"><pre>
-module(tut15).

-export([start/0, ping/2, pong/0]).

ping(0, Pong_PID) -&#62;
    Pong_PID ! finished,
    io:format(&#34;ping finished~n&#34;, []);

ping(N, Pong_PID) -&#62;
    Pong_PID ! {ping, self()},
    receive
        pong -&#62;
            io:format(&#34;Ping received pong~n&#34;, [])
    end,
    ping(N - 1, Pong_PID).

pong() -&#62;
    receive
        finished -&#62;
            io:format(&#34;Pong finished~n&#34;, []);
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong()
    end.

start() -&#62;
    Pong_PID = spawn(tut15, pong, []),
    spawn(tut15, ping, [3, Pong_PID]).
</pre></div>

<div class="example"><pre>
1&#62; <strong>c(tut15).</strong>
{ok,tut15}
2&#62; <strong>tut15: start().</strong>
&#60;0.36.0&#62;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
ping finished
Pong finished
</pre></div>

<p>The function <span class="code">start</span> first creates a process, let's call it
&#34;pong&#34;:
</p>
<div class="example"><pre>
Pong_PID = spawn(tut15, pong, [])
</pre></div>

<p>This process executes <span class="code">tut15:pong()</span>. <span class="code">Pong_PID</span> is
the process identity of the &#34;pong&#34; process. The function
<span class="code">start</span> now creates another process &#34;ping&#34;.
</p>
<div class="example"><pre>
spawn(tut15, ping, [3, Pong_PID]),
</pre></div>

<p>this process executes
</p>
<div class="example"><pre>
tut15:ping(3, Pong_PID)
</pre></div>

<p>&#60;0.36.0&#62; is the return value from the <span class="code">start</span> function.
</p>
<p>The process &#34;pong&#34; now does:
</p>
<div class="example"><pre>
receive
    finished -&#62;
        io:format(&#34;Pong finished~n&#34;, []);
    {ping, Ping_PID} -&#62;
        io:format(&#34;Pong received ping~n&#34;, []),
        Ping_PID ! pong,
        pong()
end.
</pre></div>

<p>The <span class="code">receive</span> construct is used to allow processes to wait
for messages from other processes. It has the format:
</p>
<div class="example"><pre>
receive
   pattern1 -&#62;
       actions1;
   pattern2 -&#62;
       actions2;
   ....
   patternN
       actionsN
end.
</pre></div>

<p>Note: no &#34;;&#34; before the <span class="code">end</span>.
</p>
<p>Messages between Erlang processes are simply valid Erlang terms.
I.e. they can be lists, tuples, integers, atoms, pids etc.
</p>
<p>Each process has its own input queue for messages it receives.
New messages received are put at the end of the queue. When a
process executes a <span class="code">receive</span>, the first message in the queue
is matched against the first pattern in the <span class="code">receive</span>, if
this matches, the message is removed from the queue and
the actions corresponding to the the pattern are executed.
</p>
<p>However, if the first pattern does not match, the second pattern
is tested, if this matches the message is removed from the queue
and the actions corresponding to the second pattern are executed.
If the second pattern does not match the third is tried and so on
until there are no more pattern to test. If there are no more
patterns to test, the first message is kept in the queue and we
try the second message instead. If this matches any pattern,
the appropriate actions are executed and the second message is
removed from the queue (keeping the first message and any other
messages in the queue). If the second message does not match we
try the third message and so on until we reach the end of
the queue. If we reach the end of the queue, the process blocks
(stops execution) and waits until a new message is received and
this procedure is repeated.
</p>
<p>Of course the Erlang implementation is &#34;clever&#34; and minimizes
the number of times each message is tested against the patterns
in each <span class="code">receive</span>.
</p>
<p>Now back to the ping pong example.
</p>
<p>&#34;Pong&#34; is waiting for messages. If the atom <span class="code">finished</span> is
received, &#34;pong&#34; writes &#34;Pong finished&#34; to the output and as it
has nothing more to do, terminates. If it receives a message with
the format:
</p>
<div class="example"><pre>
{ping, Ping_PID}
</pre></div>

<p>it writes &#34;Pong received ping&#34; to the output and sends the atom
<span class="code">pong</span> to the process &#34;ping&#34;:
</p>
<div class="example"><pre>
Ping_PID ! pong
</pre></div>

<p>Note how the operator &#34;!&#34; is used to send messages. The syntax
of &#34;!&#34; is:
</p>
<div class="example"><pre>
Pid ! Message
</pre></div>

<p>I.e. <span class="code">Message</span> (any Erlang term) is sent to the process
with identity <span class="code">Pid</span>.
</p>
<p>After sending the message <span class="code">pong</span>, to the process &#34;ping&#34;,
&#34;pong&#34; calls the <span class="code">pong</span> function again, which causes it to
get back to the <span class="code">receive</span> again and wait for another message.
Now let's look at the process &#34;ping&#34;. Recall that it was started
by executing:
</p>
<div class="example"><pre>
tut15:ping(3, Pong_PID)
</pre></div>

<p>Looking at the function <span class="code">ping/2</span> we see that the second
clause of <span class="code">ping/2</span> is executed since the value of the first
argument is 3 (not 0) (first clause head is
<span class="code">ping(0,Pong_PID)</span>, second clause head is
<span class="code">ping(N,Pong_PID)</span>, so <span class="code">N</span> becomes 3).
</p>
<p>The second clause sends a message to &#34;pong&#34;:
</p>
<div class="example"><pre>
Pong_PID ! {ping, self()},
</pre></div>

<p><span class="code">self()</span> returns the pid of the process which executes
<span class="code">self()</span>, in this case the pid of &#34;ping&#34;. (Recall the code
for &#34;pong&#34;, this will land up in the variable <span class="code">Ping_PID</span> in
the <span class="code">receive</span> previously explained).
</p>
<p>&#34;Ping&#34; now waits for a reply from &#34;pong&#34;:
</p>
<div class="example"><pre>
receive
    pong -&#62;
        io:format(&#34;Ping received pong~n&#34;, [])
end,
</pre></div>

<p>and writes &#34;Ping received pong&#34; when this reply arrives, after
which &#34;ping&#34; calls the <span class="code">ping</span> function again.
</p>
<div class="example"><pre>
ping(N - 1, Pong_PID)
</pre></div>

<p><span class="code">N-1</span> causes the first argument to be decremented until it
becomes 0. When this occurs, the first clause of <span class="code">ping/2</span>
will be executed:
</p>
<div class="example"><pre>
ping(0, Pong_PID) -&#62;
    Pong_PID !  finished,
    io:format(&#34;ping finished~n&#34;, []);
</pre></div>

<p>The atom <span class="code">finished</span> is sent to &#34;pong&#34; (causing it to
terminate as described above) and &#34;ping finished&#34; is written to
the output. &#34;Ping&#34; then itself terminates as it has nothing left
to do.
</p><a name="3.3"><!-- Empty --></a>
<h3>3.3 Registered Process Names</h3>

<p>In the above example, we first created &#34;pong&#34; so as to be able
to give the identity of &#34;pong&#34; when we started &#34;ping&#34;. I.e. in
some way &#34;ping&#34; must be able to know the identity of &#34;pong&#34; in
order to be able to send a message to it. Sometimes processes
which need to know each others identities are started completely
independently of each other. Erlang thus provides a mechanism for
processes to be given names so that these names can be used as
identities instead of pids. This is done by using
the <span class="code">register</span> BIF:
</p>
<div class="example"><pre>
register(some_atom, Pid)
</pre></div>

<p>We will now re-write the ping pong example using this and giving
the name <span class="code">pong</span> to the &#34;pong&#34; process:
</p>
<div class="example"><pre>
-module(tut16).

-export([start/0, ping/1, pong/0]).

ping(0) -&#62;
    pong ! finished,
    io:format(&#34;ping finished~n&#34;, []);

ping(N) -&#62;
    pong ! {ping, self()},
    receive
        pong -&#62;
            io:format(&#34;Ping received pong~n&#34;, [])
    end,
    ping(N - 1).

pong() -&#62;
    receive
        finished -&#62;
            io:format(&#34;Pong finished~n&#34;, []);
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong()
    end.

start() -&#62;
    register(pong, spawn(tut16, pong, [])),
    spawn(tut16, ping, [3]).
</pre></div>

<div class="example"><pre>
2&#62; <strong>c(tut16).</strong>
{ok, tut16}
3&#62; <strong>tut16:start().</strong>
&#60;0.38.0&#62;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
ping finished
Pong finished
</pre></div>

<p>In the <span class="code">start/0</span> function,
</p>
<div class="example"><pre>
register(pong, spawn(tut16, pong, [])),
</pre></div>

<p>both spawns the &#34;pong&#34; process and gives it the name <span class="code">pong</span>.
In the &#34;ping&#34; process we can now send messages to <span class="code">pong</span> by:
</p>
<div class="example"><pre>
pong ! {ping, self()},
</pre></div>

<p>so that <span class="code">ping/2</span> now becomes <span class="code">ping/1</span> as we don't have
to use the argument <span class="code">Pong_PID</span>.
</p><a name="3.4"><!-- Empty --></a>
<h3>3.4 Distributed Programming</h3>

<p>Now let's re-write the ping pong program with &#34;ping&#34; and &#34;pong&#34;
on different computers. Before we do this, there are a few things
we need to set up to get this to work. The distributed Erlang
implementation provides a basic security mechanism to prevent
unauthorized access to an Erlang system on another computer
(*manual*). Erlang systems which talk to each other must have
the same <strong>magic cookie</strong>. The easiest way to achieve this
is by having a file called <span class="code">.erlang.cookie</span> in your home
directory on all machines which on which you are going to run
Erlang systems communicating with each other (on Windows systems
the home directory is the directory where pointed to by the $HOME
environment variable - you may need to set this. On Linux or Unix
you can safely ignore this and simply create a file called
<span class="code">.erlang.cookie</span> in the directory you get to after executing
the command <span class="code">cd</span> without any argument).
The <span class="code">.erlang.cookie</span> file should contain on line with
the same atom. For example on Linux or Unix in the OS shell:
</p>
<div class="example"><pre>
$ <strong>cd</strong>
$ <strong>cat &#62; .erlang.cookie</strong>
this_is_very_secret
$ <strong>chmod 400 .erlang.cookie</strong>
</pre></div>

<p>The <span class="code">chmod</span> above make the <span class="code">.erlang.cookie</span> file
accessible only by the owner of the file. This is a requirement.
</p>
<p>When you start an Erlang system which is going to talk to other
Erlang systems, you must give it a name, eg: 
</p>
<div class="example"><pre>
$ <strong>erl -sname my_name</strong>
</pre></div>

<p>We will see more details of this later (*manual*). If you want to
experiment with distributed Erlang, but you only have one
computer to work on, you can start two separate Erlang systems on
the same computer but give them different names. Each Erlang
system running on a computer is called an Erlang node.
</p>
<p>(Note: <span class="code">erl -sname</span> assumes that all nodes are in the same
IP domain and we can use only the first component of the IP
address, if we want to use nodes in different domains we use
<span class="code">-name</span> instead, but then all IP address must be given in
full (*manual*).
</p>
<p>Here is the ping pong example modified to run on two separate
nodes:
</p>
<div class="example"><pre>
-module(tut17).

-export([start_ping/1, start_pong/0,  ping/2, pong/0]).

ping(0, Pong_Node) -&#62;
    {pong, Pong_Node} ! finished,
    io:format(&#34;ping finished~n&#34;, []);

ping(N, Pong_Node) -&#62;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&#62;
            io:format(&#34;Ping received pong~n&#34;, [])
    end,
    ping(N - 1, Pong_Node).

pong() -&#62;
    receive
        finished -&#62;
            io:format(&#34;Pong finished~n&#34;, []);
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong()
    end.

start_pong() -&#62;
    register(pong, spawn(tut17, pong, [])).

start_ping(Pong_Node) -&#62;
    spawn(tut17, ping, [3, Pong_Node]).
</pre></div>

<p>Let us assume we have two computers called gollum and kosken. We
will start a node on kosken called ping and then a node on gollum
called pong.
</p>
<p>On kosken (on a Linux/Unix system):
</p>
<div class="example"><pre>
kosken&#62; <strong>erl -sname ping</strong>
Erlang (BEAM) emulator version 5.2.3.7 [hipe] [threads:0]

Eshell V5.2.3.7  (abort with ^G)
(ping@kosken)1&#62;
</pre></div>

<p>On gollum:
</p>
<div class="example"><pre>
gollum&#62; <strong>erl -sname pong</strong>
Erlang (BEAM) emulator version 5.2.3.7 [hipe] [threads:0]

Eshell V5.2.3.7  (abort with ^G)
(pong@gollum)1&#62;
</pre></div>

<p>Now we start the &#34;pong&#34; process on gollum:
</p>
<div class="example"><pre>
(pong@gollum)1&#62; <strong>tut17:start_pong().</strong>
true
</pre></div>

<p>and start the &#34;ping&#34; process on kosken (from the code above you
will see that a parameter of the <span class="code">start_ping</span> function is
the node name of the Erlang system where &#34;pong&#34; is running):
</p>
<div class="example"><pre>
(ping@kosken)1&#62; <strong>tut17:start_ping(pong@gollum).</strong>
&#60;0.37.0&#62;
Ping received pong
Ping received pong 
Ping received pong
ping finished
</pre></div>

<p>Here we see that the ping pong program has run, on the &#34;pong&#34;
side we see:
</p>
<div class="example"><pre>
(pong@gollum)2&#62;
Pong received ping                 
Pong received ping                 
Pong received ping                 
Pong finished                      
(pong@gollum)2&#62;
</pre></div>

<p>Looking at the <span class="code">tut17</span> code we see that the <span class="code">pong</span>
function itself is unchanged, the lines:
</p>
<div class="example"><pre>
{ping, Ping_PID} -&#62;
    io:format(&#34;Pong received ping~n&#34;, []),
    Ping_PID ! pong,
</pre></div>

<p>work in the same way irrespective of on which node the &#34;ping&#34;
process is executing. Thus Erlang pids contain information about
where the process executes so if you know the pid of a process,
the &#34;!&#34; operator can be used to send it a message if the process
is on the same node or on a different node.
</p>
<p>A difference is how we send messages to a registered process on
another node:
</p>
<div class="example"><pre>
{pong, Pong_Node} ! {ping, self()},
</pre></div>

<p>We use a tuple <span class="code">{registered_name,node_name}</span> instead of
just the <span class="code">registered_name</span>.
</p>
<p>In the previous example, we started &#34;ping&#34; and &#34;pong&#34; from
the shells of two separate Erlang nodes. <span class="code">spawn</span> can also be
used to start processes in other nodes. The next example is
the ping pong program, yet again, but this time we will start
&#34;ping&#34; in another node:
</p>
<div class="example"><pre>
-module(tut18).

-export([start/1,  ping/2, pong/0]).

ping(0, Pong_Node) -&#62;
    {pong, Pong_Node} ! finished,
    io:format(&#34;ping finished~n&#34;, []);

ping(N, Pong_Node) -&#62;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&#62;
            io:format(&#34;Ping received pong~n&#34;, [])
    end,
    ping(N - 1, Pong_Node).

pong() -&#62;
    receive
        finished -&#62;
            io:format(&#34;Pong finished~n&#34;, []);
        {ping, Ping_PID} -&#62;
            io:format(&#34;Pong received ping~n&#34;, []),
            Ping_PID ! pong,
            pong()
    end.

start(Ping_Node) -&#62;
    register(pong, spawn(tut18, pong, [])),
    spawn(Ping_Node, tut18, ping, [3, node()]).
</pre></div>

<p>Assuming an Erlang system called ping (but not the &#34;ping&#34;
process) has already been started on kosken, then on gollum we do:
</p>
<div class="example"><pre>
(pong@gollum)1&#62; <strong>tut18:start(ping@kosken).</strong>
&#60;3934.39.0&#62;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong finished
ping finished
</pre></div>

<p>Notice we get all the output on gollum. This is because the io
system finds out where the process is spawned from and sends all
output there.
</p><a name="3.5"><!-- Empty --></a>
<h3>3.5 A Larger Example</h3>

<p>Now for a larger example. We will make an extremely simple
&#34;messenger&#34;. The messenger is a program which allows users to log
in on different nodes and send simple messages to each other.
</p>
<p>Before we start, let's note the following:
</p>
<ul>

<li>

This example will just show the message passing logic no
attempt at all has been made to provide a nice graphical user
interface - this can of course also be done in Erlang - but
that's another tutorial.<br />


</li>


<li>

This sort of problem can be solved more easily if you use
the facilities in OTP, which will also provide methods for
updating code on the fly etc. But again, that's another
tutorial.<br />


</li>


<li>

The first program we write will contain some inadequacies as
regards handling of nodes which disappear, we will correct
these in a later version of the program.<br />


</li>


</ul>

<p>We will set up the messenger by allowing &#34;clients&#34; to connect to
a central server and say who and where they are. I.e. a user
won't need to know the name of the Erlang node where another user
is located to send a message.
</p>
<p>File <span class="code">messenger.erl</span>:
</p><a name="ex"><!-- Empty --></a>
<div class="example"><pre>
%%% Message passing utility.  
%%% User interface:
%%% logon(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.
%%% logoff()
%%%     Logs off anybody at at node
%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.
%%%
%%% One node in the network of Erlang nodes runs a server which maintains
%%% data about the logged on users. The server is registered as &#34;messenger&#34;
%%% Each node where there is a user logged on runs a client process registered
%%% as &#34;mess_client&#34; 
%%%
%%% Protocol between the client processes and the server
%%% ----------------------------------------------------
%%% 
%%% To server: {ClientPid, logon, UserName}
%%% Reply {messenger, stop, user_exists_at_other_node} stops the client
%%% Reply {messenger, logged_on} logon was successful
%%%
%%% To server: {ClientPid, logoff}
%%% Reply: {messenger, logged_off}
%%%
%%% To server: {ClientPid, logoff}
%%% Reply: no reply
%%%
%%% To server: {ClientPid, message_to, ToName, Message} send a message
%%% Reply: {messenger, stop, you_are_not_logged_on} stops the client
%%% Reply: {messenger, receiver_not_found} no user with this name logged on
%%% Reply: {messenger, sent} Message has been sent (but no guarantee)
%%%
%%% To client: {message_from, Name, Message},
%%%
%%% Protocol between the &#34;commands&#34; and the client
%%% ----------------------------------------------
%%%
%%% Started: messenger:client(Server_Node, Name)
%%% To client: logoff
%%% To client: {message_to, ToName, Message}
%%%
%%% Configuration: change the server_node() function to return the
%%% name of the node where the messenger server runs

-module(messenger).
-export([start_server/0, server/1, logon/1, logoff/0, message/2, client/2]).

%%% Change the function below to return the name of the node where the
%%% messenger server runs
server_node() -&#62;
    messenger@bill.

%%% This is the server process for the &#34;messenger&#34;
%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server(User_List) -&#62;
    receive
        {From, logon, Name} -&#62;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {From, logoff} -&#62;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        {From, message_to, To, Message} -&#62;
            server_transfer(From, To, Message, User_List),
            io:format(&#34;list is now: ~p~n&#34;, [User_List]),
            server(User_List)
    end.

%%% Start the server
start_server() -&#62;
    register(messenger, spawn(messenger, server, [[]])).


%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&#62;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&#62;
            From ! {messenger, stop, user_exists_at_other_node},  %reject logon
            User_List;
        false -&#62;
            From ! {messenger, logged_on},
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&#62;
    lists:keydelete(From, 1, User_List).


%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&#62;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&#62;
            From ! {messenger, stop, you_are_not_logged_on};
        {value, {From, Name}} -&#62;
            server_transfer(From, Name, To, Message, User_List)
    end.
%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&#62;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&#62;
            From ! {messenger, receiver_not_found};
        {value, {ToPid, To}} -&#62;
            ToPid ! {message_from, Name, Message}, 
            From ! {messenger, sent} 
    end.


%%% User Commands
logon(Name) -&#62;
    case whereis(mess_client) of 
        undefined -&#62;
            register(mess_client, 
                     spawn(messenger, client, [server_node(), Name]));
        _ -&#62; already_logged_on
    end.

logoff() -&#62;
    mess_client ! logoff.

message(ToName, Message) -&#62;
    case whereis(mess_client) of % Test if the client is running
        undefined -&#62;
            not_logged_on;
        _ -&#62; mess_client ! {message_to, ToName, Message},
             ok
end.


%%% The client process which runs on each server node
client(Server_Node, Name) -&#62;
    {messenger, Server_Node} ! {self(), logon, Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&#62;
    receive
        logoff -&#62;
            {messenger, Server_Node} ! {self(), logoff},
            exit(normal);
        {message_to, ToName, Message} -&#62;
            {messenger, Server_Node} ! {self(), message_to, ToName, Message},
            await_result();
        {message_from, FromName, Message} -&#62;
            io:format(&#34;Message from ~p: ~p~n&#34;, [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&#62;
    receive
        {messenger, stop, Why} -&#62; % Stop the client 
            io:format(&#34;~p~n&#34;, [Why]),
            exit(normal);
        {messenger, What} -&#62;  % Normal response
            io:format(&#34;~p~n&#34;, [What])
    end.
</pre></div>

<p>To use this program you need to:
</p>
<ul>

<li>
configure the <span class="code">server_node()</span> function
</li>


<li>
copy the compiled code (<span class="code">messenger.beam</span>) to
the directory on each computer where you start Erlang.
</li>


</ul>

<p>In the following example of use of this program, I have started
nodes on four different computers, but if you don't have that
many machines available on your network, you could start up
several nodes on the same machine.
</p>
<p>We start up four Erlang nodes, messenger@super, c1@bilbo,
c2@kosken, c3@gollum.
</p>
<p>First we start up a the server at messenger@super:
</p>
<div class="example"><pre>
(messenger@super)1&#62; <strong>messenger:start_server().</strong>
true
</pre></div>

<p>Now Peter logs on at c1@bilbo:
</p>
<div class="example"><pre>
(c1@bilbo)1&#62; <strong>messenger:logon(peter).</strong>
true
logged_on
</pre></div>

<p>James logs on at c2@kosken:
</p>
<div class="example"><pre>
(c2@kosken)1&#62; <strong>messenger:logon(james).</strong>
true
logged_on
</pre></div>

<p>and Fred logs on at c3@gollum:
</p>
<div class="example"><pre>
(c3@gollum)1&#62; <strong>messenger:logon(fred).</strong>
true
logged_on
</pre></div>

<p>Now Peter sends Fred a message:
</p>
<div class="example"><pre>
(c1@bilbo)2&#62; <strong>messenger:message(fred, &#34;hello&#34;).</strong>
ok
sent
</pre></div>

<p>And Fred receives the message and sends a message to Peter and
logs off:
</p>
<div class="example"><pre>
Message from peter: &#34;hello&#34;
(c3@gollum)2&#62; <strong>messenger:message(peter, &#34;go away, I'm busy&#34;).</strong>
ok
sent
(c3@gollum)3&#62; <strong>messenger:logoff().</strong>
logoff
</pre></div>

<p>James now tries to send a message to Fred:
</p>
<div class="example"><pre>
(c2@kosken)2&#62; <strong>messenger:message(fred, &#34;peter doesn't like you&#34;).</strong>
ok
receiver_not_found
</pre></div>

<p>But this fails as Fred has already logged off.
</p>
<p>First let's look at some of the new concepts we have introduced.
</p>
<p>There are two versions of the <span class="code">server_transfer</span> function,
one with four arguments (<span class="code">server_transfer/4</span>) and one with
five (<span class="code">server_transfer/5</span>). These are regarded by Erlang as
two separate functions.
</p>
<p>Note how we write the <span class="code">server</span> function so that it calls
itself, <span class="code">server(User_List)</span> and thus creates a loop.
The Erlang compiler is &#34;clever&#34; and optimizes the code so that
this really is a sort of loop and not a proper function call. But
this only works if there is no code after the call, otherwise
the compiler will expect the call to return and make a proper
function call. This would result in the process getting bigger
and bigger for every loop.
</p>
<p>We use functions in the <span class="code">lists</span> module. This is a very
useful module and a study of the manual page is recommended
(<span class="code">erl -man lists</span>).
<span class="code">lists:keymember(Key,Position,Lists)</span> looks through a list
of tuples and looks at <span class="code">Position</span> in each tuple to see if it
is the same as <span class="code">Key</span>. The first element is position 1. If it
finds a tuple where the element at <span class="code">Position</span> is the same as
Key, it returns <span class="code">true</span>, otherwise <span class="code">false</span>.
</p>
<div class="example"><pre>
3&#62; <strong>lists:keymember(a, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</strong>
true
4&#62; <strong>lists:keymember(p, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</strong>
false
</pre></div>

<p><span class="code">lists:keydelete</span> works in the same way but deletes
the first tuple found (if any) and returns the remaining list:
</p>
<div class="example"><pre>
5&#62; <strong>lists:keydelete(a, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</strong>
[{x,y,z},{b,b,b},{q,r,s}]
</pre></div>

<p><span class="code">lists:keysearch</span> is like <span class="code">lists:keymember</span>, but it
returns <span class="code">{value,Tuple_Found}</span> or the atom <span class="code">false</span>.
</p>
<p>There are a lot more very useful functions in the <span class="code">lists</span>
module.
</p>
<p>An Erlang process will (conceptually) run until it does a
<span class="code">receive</span> and there is no message which it wants to receive
in the message queue. I say &#34;conceptually&#34; because the Erlang
system shares the CPU time between the active processes in
the system.
</p>
<p>A process terminates when there is nothing more for it to do,
i.e. the last function it calls simply returns and doesn't call
another function. Another way for a process to terminate is for
it to call <span class="code">exit/1</span>. The argument to <span class="code">exit/1</span> has a
special meaning which we will look at later. In this example we
will do <span class="code">exit(normal)</span> which has the same effect as a
process running out of functions to call.
</p>
<p>The BIF <span class="code">whereis(RegisteredName)</span> checks if a registered
process of name <span class="code">RegisteredName</span> exists and return the pid
of the process if it does exist or the atom <span class="code">undefined</span> if
it does not.
</p>
<p>You should by now be able to understand most of the code above
so I'll just go through one case: a message is sent from one user
to another.
</p>
<p>The first user &#34;sends&#34; the message in the example above by:
</p>
<div class="example"><pre>
messenger:message(fred, &#34;hello&#34;)
</pre></div>

<p>After testing that the client process exists:
</p>
<div class="example"><pre>
whereis(mess_client) 
</pre></div>

<p>and a message is sent to <span class="code">mess_client</span>:
</p>
<div class="example"><pre>
mess_client ! {message_to, fred, &#34;hello&#34;}
</pre></div>

<p>The client sends the message to the server by:
</p>
<div class="example"><pre>
{messenger, messenger@super} ! {self(), message_to, fred, &#34;hello&#34;},
</pre></div>

<p>and waits for a reply from the server.
</p>
<p>The server receives this message and calls:
</p>
<div class="example"><pre>
server_transfer(From, fred, &#34;hello&#34;, User_List),
</pre></div>

<p>which checks that the pid <span class="code">From</span> is in the <span class="code">User_List</span>:
</p>
<div class="example"><pre>
lists:keysearch(From, 1, User_List) 
</pre></div>

<p>If <span class="code">keysearch</span> returns the atom <span class="code">false</span>, some sort of
error has occurred and the server sends back the message:
</p>
<div class="example"><pre>
From ! {messenger, stop, you_are_not_logged_on}
</pre></div>

<p>which is received by the client which in turn does
<span class="code">exit(normal)</span> and terminates. If <span class="code">keysearch</span> returns
<span class="code">{value,{From,Name}}</span> we know that the user is logged on and
is his name (peter) is in variable <span class="code">Name</span>. We now call:
</p>
<div class="example"><pre>
server_transfer(From, peter, fred, &#34;hello&#34;, User_List)
</pre></div>

<p>Note that as this is <span class="code">server_transfer/5</span> it is not the same
as the previous function <span class="code">server_transfer/4</span>. We do another
<span class="code">keysearch</span> on <span class="code">User_List</span> to find the pid of the client
corresponding to fred:
</p>
<div class="example"><pre>
lists:keysearch(fred, 2, User_List)
</pre></div>

<p>This time we use argument 2 which is the second element in
the tuple. If this returns the atom <span class="code">false</span> we know that
fred is not logged on and we send the message:
</p>
<div class="example"><pre>
From ! {messenger, receiver_not_found};
</pre></div>

<p>which is received by the client, if <span class="code">keysearch</span> returns:
</p>
<div class="example"><pre>
{value, {ToPid, fred}}
</pre></div>

<p>we send the message:
</p>
<div class="example"><pre>
ToPid ! {message_from, peter, &#34;hello&#34;}, 
</pre></div>

<p>to fred's client and the message:
</p>
<div class="example"><pre>
From ! {messenger, sent} 
</pre></div>

<p>to peter's client.
</p>
<p>Fred's client receives the message and prints it:
</p>
<div class="example"><pre>
{message_from, peter, &#34;hello&#34;} -&#62;
    io:format(&#34;Message from ~p: ~p~n&#34;, [peter, &#34;hello&#34;])
</pre></div>

<p>and peter's client receives the message in
the <span class="code">await_result</span> function.
</p><center>
<hr/>
<small>
  Copyright &copy; 1991-2008
  <a href="http://www.ericsson.com/technology/opensource/erlang">Ericsson AB</a><br/ >
</small>
</center></body>
</html>
